def project = "tensorflow"

def buildTargets = [
  "fedora28": "registry.fedoraproject.org/f28/s2i-core",
  "centos7": "openshift/base-centos7"
]

def pythonVersions = ["3.6"]

def templates = ["tensorflow-build-image.json", "tensorflow-build-job.json"]

node {
  try {
    echo "Printing Env Vars"
    echo sh(returnStdout: true, script: 'env')

    stage("Checkout") {
      checkout scm
    }

    stage("Create Templates") {
      openshift.withCluster() {
        openshift.withProject(project) {
          templates.each { template ->
            def templateFileName = template.take(template.lastIndexOf("."))
            def templateSelector = openshift.selector("template", templateFileName)

            if (!templateSelector.exists()) {
              openshift.create(readFile(template))
            } else {
              openshift.replace(readFile(template))
            }
          }
        }
      }
    }

    stage("Tensorflow-Trigger") {
      def builds = [:]

      buildTargets.each { operatingSystem, registry ->
        pythonVersions.each { pythonVersion ->

          if (jenkins.model.Jenkins.instance.getItem("tensorflow-${operatingSystem}-${pythonVersion}") == null) {
            jobDsl  scriptText: createJob(operatingSystem, pythonVersion)
          }

          builds["tensorflow-${operatingSystem}-${pythonVersion}"] = {
            build job: "tensorflow-${operatingSystem}-${pythonVersion}",
              parameters: [
                string(name: "OPERATING_SYSTEM", value: "${operatingSystem}"),
                string(name: "REGISTRY", value: "${registry}"),
                string(name: "PYTHON_VERSION", value: "${pythonVersion}"),
              ]
          }
        }
      }

      parallel builds
    }
  } catch (e) {
    echo e.toString()
    throw e
  }
}

def createJob(operatingSystem, pythonVersion) {
  return """
    pipelineJob("tensorflow-${operatingSystem}-${pythonVersion}") {
      parameters {
        stringParam("OPERATING_SYSTEM")
        stringParam("REGISTRY")
        stringParam("PYTHON_VERSION")
      }
      definition {
        cpsScm {
          scm {
            git("https://github.com/thoth-station/tensorflow-build-s2i", "*/master")
          }
        }
      }
    }
  """
}
